#version 450

layout(local_size_x = 256) in;

layout(std430, binding = 0) buffer InOut { float a[]; };
layout(std430, binding = 1) buffer InB   { float b[]; };

layout(push_constant) uniform PC {
    uint rank;
    uint pad;
    uint dims[8];
    uint stridesA[8];
    uint stridesB[8];
} pc;

void main() {
    uint gid = gl_GlobalInvocationID.x;
    uint total = 1;
    for (uint i = 0; i < pc.rank; ++i) total *= pc.dims[i];
    if (gid >= total) return;

    uint idx = gid;
    uint offA = 0;
    for (int i = int(pc.rank) - 1; i >= 0; --i) {
        uint d = pc.dims[i];
        uint r = idx % d;
        idx /= d;
        offA += r * pc.stridesA[i];
    }

    idx = gid;
    uint offB = 0;
    for (int i = int(pc.rank) - 1; i >= 0; --i) {
        uint d = pc.dims[i];
        uint r = idx % d;
        idx /= d;
        offB += r * pc.stridesB[i];
    }

    a[offA] = min(a[offA], b[offB]);
}
